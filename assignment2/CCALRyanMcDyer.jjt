//Ryan McDyer
//13431038

options {
  JAVA_UNICODE_ESCAPE = true;
  IGNORE_CASE = true;
  NODE_DEFAULT_VOID = true;
  MULTI = true;
  VISITOR = true;
//  DEBUG_PARSER=true;
}

PARSER_BEGIN(CCAL)

import java.util.*;
import java.io.*;

public class CCAL {

  public static Hashtable ST = new Hashtable();

  public static void main(String args[]) throws ParseException {

    CCAL parser;

    if (args.length == 0) {
      System.out.println("Reading from standard input . . .");
      parser = new CCAL(System.in);
    } else if (args.length == 1) {
      try {
        parser = new CCAL(new java.io.FileInputStream(args[0]));

        SimpleNode root = parser.program();

        System.out.println("Abstract Syntax Tree:");
        root.dump(" ");

        System.out.println();

				SCVisitor visitor = new SCVisitor();
        root.jjtAccept(visitor, null);

/*
        System.out.println();
        System.out.println("Program:");
        PrintVisitor pv = new PrintVisitor();
        root.jjtAccept(pv, null);

        System.out.println();
        System.out.println("Type Checking:");
        TypeCheckVisitor tc = new TypeCheckVisitor();
        root.jjtAccept(tc, ST);    */

      } catch (java.io.FileNotFoundException e) {
        System.err.println("File " + args[0] + " not found.");
        return;
      }
    } else {
      System.out.println("Usage is one of:");
      System.out.println(" java CCAL");
      System.out.println("OR");
      System.out.println(" java CCAL inputfile");
      return;
    }
  }
}
PARSER_END(CCAL)


/*****************************************
************ TOKEN DEFINITIONS ***********
*****************************************/
TOKEN_MGR_DECLS :
{
  static int commentNesting = 0;
}
SKIP : /*** Ignoring whitespaces ***/
{
  " "
  | "\t"
  | "\n"
  | "\r"
  | "\f"
}
SKIP : // COMMENTS, taken from lth.se
{
  < "//" (~["\n","\r"])* ("\n" | "\r" | "\r\n") >
}
SKIP : /* COMMENTS */
{
  <"/*"> { commentNesting++; } : IN_COMMENT
}
<IN_COMMENT> SKIP :
{
  <"/*"> { commentNesting++; }
  | <"*/"> { commentNesting--;
    if (commentNesting == 0)
      SwitchTo(DEFAULT);
  }
  | <~[]>
}
<DEFAULT> SKIP : //The string "SKIP"
{
  <SKIP_STRING>
}

TOKEN : /* Keywords */
{
  < VAR : "var" >
  | < CONST : "const" >
  | < RETURN : "return" >
  | < INTEGER : "integer" >
  | < BOOLEAN : "boolean" >
  | < VOID : "void" >
  | < MAIN : "main" >
  | < IF : "if" >
  | < ELSE : "else" >
  | < TRUE : "true" >
  | < FALSE : "false" >
  | < WHILE : "while" >
  | < SKIP_STRING : "skip" >
}
TOKEN : /* Other reserved tokens*/
{
  < COMMA : "," >
  | < SEMIC : ";" >
  | < COLON : ":" >
  | < LBRACE : "{" >
  | < RBRACE : "}" >
  | < LPAREN : "(" >
  | < RPAREN : ")" >
}
TOKEN : /* Operators */
{
  < ASSIGN : "=" >
  | < PLUS_SIGN : "+" >
  | < MINUS_SIGN : "-" >
  | < NEGATION : "~" >
  | < OR : "||" >
  | < AND : "&&" >
  | < EQUALTO : "==" >
  | < NOTEQUALTO : "!=" >
  | < LESSTHAN : "<" >
  | < LESSTHANEQUALS : "<=" >
  | < GREATERTHAN : ">" >
  | < GREATERTHANEQUALS : ">=" >
}
TOKEN : /* Numbers and IDs */
{
  < NUMBER : <DIGIT>
    | <NONZERO> (<DIGIT>)*
    | <MINUS_SIGN> <DIGIT>
    | <MINUS_SIGN> <NONZERO> (<DIGIT>)* >
  | < #DIGIT : ["0" - "9"] >
  | < #NONZERO : ["1" - "9"] >
  | < ID : (<LETTER>)(<LETTER> | "_" | <DIGIT>)* >
  | < #LETTER : ["a" - "z", "A" - "Z"] >
}
TOKEN : /* Anything else */
{
  < OTHER : ~[] >
}
/***********************************
************ THE GRAMMAR ***********
***********************************/
SimpleNode program() #Program : {}//(1)
{
  decl_list() function_list() main() <EOF> { return jjtThis; }
}

void decl_list() : {}//(2)
{
  (decl())*
}

void decl() : {}//(3)
{
  (var_decl() | const_decl())
}

void var_decl() #Var_decl : {}//(4)
{
  <VAR> id()<COLON>type() <SEMIC>
}

void const_decl() #Const_decl : {}//(5)
{
  <CONST> id()<COLON>type() <ASSIGN> expression() <SEMIC>
}

void function_list() #FunctionList: {}//(6)
{
  (function_decl())*
}

void function_decl() #Func_decl : {}//(7)
{
  type() id() <LPAREN> param_list() <RPAREN>
  function_body()
}

void function_body() #Func_body : {}//(7a)
{
  <LBRACE>
    decl_list()
    statement_block()
    <RETURN> <LPAREN> [expression()] <RPAREN> <SEMIC>
  <RBRACE>
}

void type() #Type : {}//(8)
{
  <INTEGER> { jjtThis.value = token; }
  | <BOOLEAN> { jjtThis.value = token; }
  | <VOID> { jjtThis.value = token; }
}

/*void parameter_list() : {}//(9)
{
  (nemp_parameter_list())*
}

void nemp_parameter_list() #Param : {}//(10)
{
  id()<COLON>type() [<COMMA> nemp_parameter_list()]
}*/

void param_list() #ParamList: {}//(9)
{
  (id()<COLON>type() (<COMMA> id()<COLON>type())*)
  | {}
}

void main() #Main : {}//(11)
{
  <MAIN> <LBRACE>
    decl_list()
    statement_block()
  <RBRACE>
}

void statement_block() : {}//(12)
{
  [statement() statement_block()]
}

void statement()  : {}//(13)
{
  LOOKAHEAD(2)
  assignment()
  | functionCall() <SEMIC>
  | <LBRACE> statement_block() <RBRACE>
  | <IF> condition()
    <LBRACE> statement_block() <RBRACE>
    <ELSE>
    <LBRACE> statement_block() <RBRACE> #IfCond
  | <WHILE> condition()
    <LBRACE> statement_block() <RBRACE> #WhileCond
  | <SKIP_STRING> <SEMIC>
}

void assignment() #Assignment : {} //(13a)
{
  id() <ASSIGN> expression() <SEMIC>
}

void functionCall() #FunctionCall : {} //(13b)
{
  id() <LPAREN> arg_list() <RPAREN>
}

void expression() : {}//(14)
{
  LOOKAHEAD(3)
  functionCall() (optional_expression_bin_op())?
  | fragment() (optional_expression_bin_op())?
  | <LPAREN> expression() <RPAREN> (optional_expression_bin_op())?
}

void optional_expression_bin_op() : {}//(14a)
{
    binary_arith_op()
}

void binary_arith_op() : {}//(15)
{
  /*<PLUS_SIGN> { jjtThis.value = token; }
  | <MINUS_SIGN> { jjtThis.value = token; }*/
  <PLUS_SIGN> fragment() { jjtThis.value = new Token(PLUS_SIGN, tokenImage[PLUS_SIGN]); } #AddOperation(2)
  | <MINUS_SIGN> fragment() { jjtThis.value = new Token(MINUS_SIGN, tokenImage[MINUS_SIGN]); } #SubOperation(2)
}

void fragment() : {}//(16)
{
  id()
  | <MINUS_SIGN> id()
  | number()
  | booleanValue()
}

void id() #Id : { Token t; } //(16a)
{
  t = <ID> { jjtThis.value = token; }
}

void number() #Number : { Token t; } //(16b)
{
  t = <NUMBER> { jjtThis.value = token; }
}

void booleanValue() #Boolean : {} //(16c)
{
  <TRUE> { jjtThis.value = token; }
  | <FALSE> { jjtThis.value = token; }
}

void condition() : {}//(17)
{
  <NEGATION> condition() optional_condition_bin_op()
  | LOOKAHEAD(3)
    <LPAREN> condition() <RPAREN> optional_condition_bin_op()
  | expression() comp_op() optional_condition_bin_op()
}

void optional_condition_bin_op() : {}//(17a)
{
  (LOOKAHEAD(2)
    binary_logical_op())?
}

void binary_logical_op() #Logic_Op: {}//(17b)
{
  <OR> condition() { jjtThis.value = token; }
  | <AND> condition() { jjtThis.value = token; }
}

void comp_op() #Compare_Op : {}//(18)
{
  <EQUALTO> expression() { jjtThis.value = token; }
  | <NOTEQUALTO> expression() { jjtThis.value = token; }
  | <LESSTHAN> expression() { jjtThis.value = token; }
  | <LESSTHANEQUALS> expression() { jjtThis.value = token; }
  | <GREATERTHAN> expression() { jjtThis.value = token; }
  | <GREATERTHANEQUALS> expression() { jjtThis.value = token; }
}

void arg_list() #ArgList: {}//(19)
{
  (id() (<COMMA> id())*)
  | {}
}
